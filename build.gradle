apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
group 'com.notronix'
version '1.0.003'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.notronix:AlbacoreDeed:2.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.hibernate:hibernate-core:5.2.13.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.2.13.Final'
    compile 'org.hibernate:hibernate-c3p0:5.2.13.Final'

    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Notronix'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name rootProject.name
                packaging 'jar'
                description 'A bridge to Hibernate 5 that simplifies the bootstrap procedure for Hibernate projects using stateless sessions.'
                url 'https://github.com/Notronix/HIbermate/'

                scm {
                    url 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                    connection 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                    developerConnection 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'clint.munden'
                        name 'Clint Munden'
                    }
                }
            }
        }
    }
}
